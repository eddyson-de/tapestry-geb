plugins {
  id "java"
  id "groovy"
  id "maven-publish"
}

description = "Utilities for Geb-based browser tests of Tapestry applications"

group = "de.eddyson"
version = "0.48.0"

repositories {
  mavenCentral()
}

java {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11

  withSourcesJar()
}

ext.versions = [
  tapestry: '5.8.3',
  jetty: '12.0.4'
]

dependencies {
  implementation "org.apache.tapestry:tapestry-core:$versions.tapestry"
  implementation "org.eclipse.jetty:jetty-server:$versions.jetty"
  implementation "org.eclipse.jetty.ee8:jetty-ee8-webapp:$versions.jetty" // servlet 4.x
  implementation "org.gebish:geb-spock:6.0"
  implementation "org.seleniumhq.selenium:selenium-support:4.15.0"

  testImplementation "io.github.bonigarcia:webdrivermanager:5.6.2"
  testImplementation "org.seleniumhq.selenium:selenium-firefox-driver:4.15.0"
  testImplementation "org.tynamo:tapestry-resteasy:0.7.0"
}

test {
  useJUnitPlatform()

  systemProperty "webappLocation", "src/test/webapp/"
  systemProperty "jettyPort", "40258"
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
    }
  }
}
